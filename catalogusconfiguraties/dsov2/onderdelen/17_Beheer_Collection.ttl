#
#
#
# BEHEER COLLECTION
#
#
#
#
# PRODUCTIONS
#

# De form laat alle members van alle geldige collecties zien, voor het LDT 2.0 zouden we hier liever een afhankelijkheid van de geselecteerde collectie in willen bouwen.

updatestage:deletevaluescollection a elmo:Production;
  elmo:contains stage:DSOv2Beheermenu;
	elmo:contains stage:DSOv2Header;
	elmo:contains stage:DSOv2Footer;
	elmo:queryForm stage:DeleteWaardenCollectionForm;
  elmo:contains stage:SelectCollection;
  elmo:contains stage:CollectionMemberList;
  elmo:contains stage:ClearSingleMemberScene
.

stage:DeleteWaardenCollectionForm a elmo:Form;
	rdfs:label "Verwijder waarde uit collectie";
  elmo:index "2";
  elmo:fragment [
      elmo:applies-to "collectie";
      rdfs:label "Selecteer de collectielijst"@nl;
      elmo:valuesFrom stage:SelectCollection;
      elmo:constraint elmo:MandatoryConstraint;
      elmo:index "3";
  ];
  elmo:fragment [
      elmo:applies-to "concept";
      rdfs:label "Selecteer het concept"@nl;
      elmo:valuesFrom stage:CollectionMemberList;
      elmo:constraint elmo:MandatoryConstraint;
      elmo:index "4";
  ];
  elmo:fragment [
      elmo:appearance elmo:SubmitAppearance;
      rdfs:label "Ga verder"@nl;
      elmo:index "5";
  ]
.

stage:CollectionMemberList a elmo:Part;
  elmo:appearance elmo:HiddenAppearance;
  elmo:query '''
    prefix adms:            <http://www.w3.org/ns/adms#>
    prefix adms-status:     <http://purl.org/adms/status/>
    prefix dcmiperiod:      <http://dublincore.org/documents/2006/04/10/dcmi-period/>
    prefix dcterms:    		  <http://purl.org/dc/terms/>
    prefix foaf: 	  		    <http://xmlns.com/foaf/0.1/>
    prefix org:             <http://www.w3.org/TR/vocab-org#>
		prefix prov:            <http://www.w3.org/ns/prov#>
		prefix skos:            <http://www.w3.org/2004/02/skos/core#>
    
		CONSTRUCT {
			?concept rdfs:label ?concept_label
		}
		WHERE {
      GRAPH ?collectionGraph {
        ?collection  skos:member ?concept;
          foaf:isPrimaryTopicOf ?docCollection
        .
      }
      FILTER regex( ?collectionGraph, "http://catalogus.test.kadaster.nl/id/transactie/" )
      GRAPH ?conceptGraph {
        ?concept rdfs:label ?concept_label;
          foaf:isPrimaryTopicOf ?docConcept
        .
      }
      FILTER regex( ?conceptGraph, "http://catalogus.test.kadaster.nl/id/transactie/" )
      GRAPH ?docCollectionGraph {
        ?docCollection rdf:type prov:Entity;
          adms:status adms-status:Completed;
          dcterms:temporal/dcmiperiod:start ?collectionStart
        .
        FILTER( strdt( ?collectionStart, xsd:dateTime ) <= now() )
        FILTER NOT EXISTS {
          ?docCollection dcterms:temporal/dcmiperiod:end ?collectionEnd.
          FILTER( strdt( ?collectionEnd, xsd:dateTime ) < now() )
        }
        ?docCollectionGraph prov:generatedAtTime ?docCollectionTime.
        FILTER NOT EXISTS {
          GRAPH ?newerDocCollectionGraph {
            ?docCollection rdf:type prov:Entity.
            ?newerDocCollectionGraph prov:generatedAtTime ?newerDocCollectionTime.
            FILTER( ?newerDocCollectionTime > ?docCollectionTime )
          }
        }  
      }
      GRAPH ?docConceptGraph {
        ?docConcept rdf:type prov:Entity;
          adms:status adms-status:Completed;
          dcterms:temporal/dcmiperiod:start ?conceptStart
        .
        FILTER( strdt( ?conceptStart, xsd:dateTime ) <= now() )
        FILTER NOT EXISTS {
          ?docConcept dcterms:temporal/dcmiperiod:end ?conceptEnd.
          FILTER( strdt( ?conceptEnd, xsd:dateTime ) < now() )
        }
        
        ?docConceptGraph prov:generatedAtTime ?docConceptTime.
        FILTER NOT EXISTS {
          GRAPH ?newerDocConceptGraph {
            ?docConcept rdf:type prov:Entity.
            ?newerDocConceptGraph prov:generatedAtTime ?newerDocConceptTime.
            FILTER( ?newerDocConceptTime > ?docConceptTime )
          }
        }  
      }    
    }
    
  '''
.  

stage:SelectCollection a elmo:Part;
	elmo:appearance elmo:HiddenAppearance;
	elmo:query '''
    prefix adms:            <http://www.w3.org/ns/adms#>
    prefix adms-status:     <http://purl.org/adms/status/>
    prefix dcmiperiod:      <http://dublincore.org/documents/2006/04/10/dcmi-period/>
    prefix dcterms:    		  <http://purl.org/dc/terms/>
    prefix foaf: 	  		    <http://xmlns.com/foaf/0.1/>
    prefix org:             <http://www.w3.org/TR/vocab-org#>
		prefix prov:            <http://www.w3.org/ns/prov#>
		prefix skos:            <http://www.w3.org/2004/02/skos/core#>
		
		CONSTRUCT {
			?collection rdfs:label ?label.
		}
		WHERE {
      GRAPH ?collectionGraph {
        ?collection rdf:type skos:Collection;
          rdfs:label ?label;
          foaf:isPrimaryTopicOf ?docCollection
        .
      }
      FILTER regex( ?collectionGraph, "http://catalogus.test.kadaster.nl/id/transactie/" )
      GRAPH ?docCollectionGraph {
        ?docCollection rdf:type prov:Entity;
          adms:status adms-status:Completed;
          dcterms:temporal/dcmiperiod:start ?collectionStart
        .
        FILTER( strdt( ?collectionStart, xsd:dateTime ) <= now() )
        FILTER NOT EXISTS {
          ?docCollection dcterms:temporal/dcmiperiod:end ?collectionEnd.
          FILTER( strdt( ?collectionEnd, xsd:dateTime ) < now() )
        }
        
       ?docCollectionGraph prov:generatedAtTime ?docCollectionTime.
        FILTER NOT EXISTS {
          GRAPH ?newerDocCollectionGraph {
            ?docCollection rdf:type prov:Entity.
            ?newerDocCollectionGraph prov:generatedAtTime ?newerDocCollectionTime.
            FILTER( ?newerDocCollectionTime > ?docCollectionTime )
          }
        }  
      }
    }
    
	'''
.

# Scene gebruikt voor het verwijderen van alleen de geslecteerde skos:member
stage:ClearSingleMemberScene a elmo:Scene;
	elmo:index "1";
	rdfs:label "Verwijder enkele member uit collectie";
	elmo:query '''
    prefix adms:            <http://www.w3.org/ns/adms#>
    prefix adms-status:     <http://purl.org/adms/status/>
    prefix dcat: 	  		    <http://www.w3.org/ns/dcat#>
    prefix dcterms:    		  <http://purl.org/dc/terms/>
    prefix foaf: 	  		    <http://xmlns.com/foaf/0.1/>
    prefix prov:            <http://www.w3.org/ns/prov#>
    prefix sd:              <http://www.w3.org/ns/sparql-service-description/>
    prefix skos:            <http://www.w3.org/2004/02/skos/core#>
    prefix skoslex:         <http://bp4mc2.org/def/skos-lex/>
    prefix skos-lex-status: <http://bp4mc2.org/skos-lex/status/>
    
    prefix container:       <http://localhost:8080/catalogus/dsov2/container/>
    prefix user:            <http://catalogus.test.kadaster.nl/users/>
 
    INSERT {
      GRAPH ?transactie {
        ?transactie rdf:type sd:Graph, prov:Entity;
          skos:changeNote "Verwijderen van een concept uit een collectie";
          prov:generatedAtTime ?now;
          prov:wasAttributedTo user:TomcatUser
        .
        <@COLLECTIE@>  ?p ?o;
          skos:member ?concept;
          foaf:isPrimaryTopicOf ?doc
        .

        ?doc rdf:type prov:Entity;
          adms:status adms-status:UnderDevelopment;
          prov:wasRevisionOf ?currentDoc
        .

        ?udDoc ?docP ?docO;
          adms:status adms-status:Withdrawn
        .
      }
    }
    WHERE {
      GRAPH ?graph {
        <@COLLECTIE@> ?p ?o;
          skos:member ?concept;
          rdf:type ?type;
          foaf:isPrimaryTopicOf ?currentDoc
        .
        FILTER( ?p != skos:member )
        FILTER( ?concept != <@CONCEPT@>)
        
        ?graph prov:generatedAtTime ?graphTime.
        FILTER NOT EXISTS {
          GRAPH ?newerGraph {
            ?newerGraph prov:generatedAtTime ?newerTime.
            <@COLLECTIE@> rdf:type skos:Collection.
            FILTER( ?newerTime > ?graphTime )
          }
        }
      }
      FILTER regex( ?graph, "http://catalogus.test.kadaster.nl/id/transactie/" )
      OPTIONAL {
        GRAPH ?udGraph {
          <@COLLECTIE@>  foaf:isPrimaryTopicOf ?udDoc.
          ?udDoc ?docP ?docO;
            adms:status adms-status:UnderDevelopment
          .
          FILTER( ?docP != adms:status )    
          ?udGraph prov:generatedAtTime ?udGraphTime.         
          FILTER NOT EXISTS {
            GRAPH ?newerDocGraph {
              ?udDoc rdf:type prov:Entity.
              ?newerDocGraph prov:generatedAtTime ?newerDocGraphTime.
              FILTER( ?newerDocGraphTime > ?udGraphTime )            
            }
          }
        } 
        FILTER regex( ?udGraph, "http://catalogus.test.kadaster.nl/id/transactie/" )
      }
      
      BIND( iri( concat( "http://catalogus.test.kadaster.nl/id/transactie/", replace( now(), " ", "T" ) ) ) AS ?transactie )
      BIND( lcase( replace( replace( str( ?type ), ".+/", "" ), ".+#", "" ) ) AS ?typename )
      BIND( iri ( concat(  "http://catalogus.test.kadaster.nl/id/entity/", replace( now(), " ", "T" ), "-", ?typename ) ) AS ?doc )
      BIND( now() AS ?now )
    }
    
	'''
.

