#
# PRODUCTION

# production om subset in named graph te plaatsen en te tonen voor download
updatestage:TaxExportConcepten a elmo:Production;
	elmo:contains stage:TaxBeheermenu;
	elmo:contains stage:TaxFooter;
	elmo:queryForm stage:TaxSelectDataset;
	elmo:contains stage:ExportSubsetBegrippenQueryScene;
.

stage:ExportSubsetBegrippenQueryScene rdf:type elmo:Representation;
	elmo:url-pattern "/admin/exportsubsetbegrippen(|/|\\..+)$";
	elmo:queryForm stage:TaxSelectDataset;
	elmo:appearance elmo:ContentAppearance;
	elmo:index "1";
	elmo:query '''
		prefix prov:	<http://www.w3.org/ns/prov#>
		prefix kddef:	<http://kadaster.basisregistraties.overheid.nl/som/def#>
		prefix kdstat:	<http://kadaster.basisregistraties.overheid.nl/id/status/>
		prefix doc:		<http://localhost:8080/catalogus/tax/concepten/doc/>
		prefix skos:	<http://www.w3.org/2004/02/skos/core#>
		prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
		prefix skoslex: <http://bp4mc2.org/def/skos-lex/>
		prefix thes: <http://purl.org/iso25964/skos-thes#>
		prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
    prefix dct: <http://purl.org/dc/terms/>
    prefix dc: <http://purl.org/dc/elements/1.1/>
    
    CONSTRUCT {
			?concept ?p ?o.
               #dct:source ?osource.
      ?o dct:source ?osource;
         rdfs:label ?olabel;
         rdfs:seeAlso ?oseealso.
 		}
		WHERE {
			GRAPH ?g {
				?concept ?p ?o.
				?concept kddef:status ?status.
        # OPTIONAL {
          # ?concept dc:source/dct:source ?source.
        # }
				FILTER NOT EXISTS {
					?concept prov:invalidatedAtTime ?time.
				}
				FILTER (
					?status = kdstat:Gevalideerd ||
					?status = kdstat:Geverifieerd
				)
        #FILTER( ?p != dc:source )
			}
			GRAPH doc:mastergraph {
				?concept rdfs:isDefinedBy ?g.
				?concept skos:inScheme <@DATASET@>.
			}
			OPTIONAL {
				GRAPH ?og {
					?o rdf:type ?otype.
					OPTIONAL {?o kddef:status ?ostatus.}
          OPTIONAL {?o dct:source ?osource.}
          OPTIONAL {?o rdfs:label ?olabel.}
          OPTIONAL {?o rdfs:seeAlso ?oseealso.}
				}
				GRAPH doc:mastergraph {
					?o rdfs:isDefinedBy ?og.
				}
			}
			FILTER(
				!isIRI( ?o )
				||
				(
					BOUND( ?otype )
					&& BOUND( ?ostatus )
					&& (
						?ostatus = kdstat:Gevalideerd
						|| ?ostatus = kdstat:Geverifieerd
					)
				)
        || !BOUND(?ostatus)
			)
		}
	'''
    
.