#
#
#
# SEARCH
#
#
#



#
# PAGINA'S (REPRESENTATIONS)
#

# Standaard zoekpagina
stage:TaxSearch a elmo:Representation;
	elmo:url-pattern "query/search";
	elmo:contains stage:TaxMenu;
	elmo:contains stage:TaxFooter;
	elmo:contains stage:TaxIndexConcepten;
	elmo:query stage:TaxSearchQuery;
.


#
# QUERY'S
#

# Zoekquery
stage:TaxSearchQuery a elmo:Query;
	elmo:query '''
		prefix prov: 		<http://www.w3.org/ns/prov#>
		prefix skoslex: 	<http://bp4mc2.org/def/skos-lex/>
		prefix container: 	<http://localhost:8080/catalogus/tax/container/>
		prefix doc: 		<http://localhost:8080/catalogus/tax/concepten/doc/>
		prefix dataset: 	<http://localhost:8080/catalogus/tax/id/dataset/>
		prefix concept: 	<http://localhost:8080/catalogus/tax/id/concept/>
		prefix kddef:		<http://kadaster.basisregistraties.overheid.nl/som/def#>
		prefix kdstat:		<http://kadaster.basisregistraties.overheid.nl/id/status/>
		
		SELECT ?concept ?concept_label ?uitleg
		WHERE {
			GRAPH ?g {
				{
					?concept rdf:type skos:Concept.
					?concept rdfs:label ?concept_label.
					?concept kddef:status ?status.
					OPTIONAL {
				#		?concept skos:definition ?uitleg
						?concept rdfs:comment ?uitleg
					}
				}
				UNION
				{
					?concept rdf:type skos:Concept.
					?concept skos:altLabel ?concept_label.
					?concept kddef:status ?status.
					OPTIONAL {
						?concept rdfs:label ?clabel.
				#		?concept skos:definition ?definition.
						?concept rdfs:comment ?definition.
						BIND( CONCAT( "(Synoniem voor: ", ?clabel, ") ", ?definition ) AS ?uitleg )
					}
				}
				FILTER regex(?concept_label, '@TERM@', 'i').
				FILTER NOT EXISTS {
					?concept prov:invalidatedAtTime ?time.
				}
				FILTER (
					?status = kdstat:Gevalideerd ||
					?status = kdstat:Geverifieerd
				)
				FILTER( LANG( ?concept_label ) = "" || LANGMATCHES( LANG( ?concept_label ), "nl" ) )
			}
			GRAPH doc:mastergraph {
				?concept rdfs:isDefinedBy ?g
			}
		}
		ORDER BY str(lcase(?concept_label))
		LIMIT 100
	''';
.
