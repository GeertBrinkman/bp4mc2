#
#
#
# BEHEER CONCEPTEN
#
#
#

#
# CONTAINERS
#

container:vragen a elmo:Container;
	elmo:contains stage:BAGBeheermenu;
	elmo:contains stage:BAGHeader;
	elmo:contains stage:BAGFooter;
	elmo:representation elmo:UploadRepresentation;
	elmo:translator elmo:DMNTranslator;
	rdfs:label "Upload vragen (.xml)";
	elmo:assertion [
		elmo:assert-not '''
			prefix container: <http://localhost:8080/catalogus/bag/container/>
			prefix dmn: <http://www.omg.org/spec/DMN/20151101/dmn#>
			
			ASK {
				GRAPH container:vragen {
					?s rdf:type dmn:Decision.
					?s dmn:question ?question.
					FILTER NOT EXISTS {
						?s dmn:description ?description.
					}
				}
			}
		''';
		rdfs:label "Elke vraag moet een toelichting bevatten."
	];
	elmo:query stage:BAGVragenUploadScenePUT
.

#
# SCENES
#

# Standaardquery voor updatecontainers
# Algoritme:
#
# - Verwijder voor de zekerheid de @CURRENTMOMENT@ GRAPH
# - Vul mastergraph aan met nieuwe versie
# - Voeg metadata toe aan nieuwe conceptengraph, activiteitengraph en actorengraph
# - Hash genereren, zodat we kunnen vergelijken of er iets gewijzigd is aan een concept
# - Update de aanpasdatum van de Catalogus, CatalogRecord en Dataset
# - Voeg in de nieuwe GRAPH de concepten toe die een andere hash dan de hash in de mastergraph hebben (d.w.z., die gewijzigd zijn)
# - Voeg voor de gewijzigde concepten in de oude GRAPH wat metadata toe (prov:invalidatedAtTime)
# - Verwijder de concepten uit de mastergraph die voorkomen in de nieuwe GRAPH (d.w.z. gewijzigd zijn)
# - Voeg een verwijzing toe in de mastergraph naar de nieuwe GRAPH voor de concepten die in de nieuwe GRAPH staan
# - Verwijder de (tijdelijke) graphs die gebruikt zijn door de uploadcontainers

stage:BAGVragenUploadScenePUT a elmo:Scene;
	rdfs:label "Postquery inladen gegevens";
	elmo:index "2";
	elmo:query '''
		prefix prov: <http://www.w3.org/ns/prov#>
		prefix skos: <http://www.w3.org/2004/02/skos/core#>
		prefix skosxl: <http://www.w3.org/2008/05/skos-xl#>
		prefix dct: <http://purl.org/dc/terms/>
		prefix dctype: <http://purl.org/dc/dcmitype/>
		prefix bp4mc2: <http://bp4mc2.org/elmo/def/>
		prefix foaf: <http://xmlns.com/foaf/0.1/>
		prefix skoslex: <http://bp4mc2.org/def/skos-lex/>
		prefix owl: <http://www.w3.org/2002/07/owl#>
		prefix container: <http://localhost:8080/catalogus/bag/container/>
		prefix doc: <http://localhost:8080/catalogus/bag/concepten/doc/>
		prefix loc: <http://localhost:8080/catalogus/bag/id/>
		prefix versiongraph: <http://localhost:8080/catalogus/bag/concepten/doc/@CURRENTMOMENT@>
		prefix usergraph: <http://localhost:8080/catalogus/bag/>
		prefix dataset: <http://localhost:8080/catalogus/bag/id/dataset/>
		prefix concept: <http://localhost:8080/catalogus/bag/id/concept/>
		prefix activity: <http://localhost:8080/catalogus/bag/id/concept/@CURRENTMOMENT@>
		prefix dmn: <http://www.omg.org/spec/DMN/20151101/dmn#>	
	
		CLEAR GRAPH versiongraph:version
		
		INSERT {
			GRAPH container:generiek {
				?s ?p ?o.
			}
		} WHERE {
			SELECT  iri(replace(str(?subject),"urn:uuid:","http://localhost:8080/catalogus/bag/id/decision/")) as ?s
					?p
					iri(replace(str(?object),"urn:uuid:","http://localhost:8080/catalogus/bag/id/decision/")) as ?o
			WHERE {
				GRAPH container:vragen {
					?subject ?p ?object
				}
				FILTER(
					isIRI(?object)
					&& ?p != rdf:type
				)
			}
		}
		
		INSERT {
			GRAPH container:generiek {
				?s ?p ?o.
			}
		} WHERE {
			SELECT  iri(replace(str(?subject),"urn:uuid:","http://localhost:8080/catalogus/bag/id/decision/")) as ?s
					?p
					?o
			WHERE {
				GRAPH container:vragen {
					?subject ?p ?o
				}
				FILTER(
					!isIRI(?o)
					|| ?p = rdf:type
				)
			}
		}
		
		INSERT {
			GRAPH container:generiek {
				?s rdfs:label ?spreflabel.
			}
		} WHERE {
			GRAPH container:generiek {
				?s dct:title ?spreflabel.
				FILTER NOT EXISTS {
					?s rdfs:label ?slabel.
				}
			}
		}
		
		INSERT {
			GRAPH doc:mastergraph {
				doc:mastergraph rdf:type void:Dataset.
				doc:mastergraph rdfs:label "Concepten".
				versiongraph:version dct:isVersionOf doc:mastergraph.
				dataset:concepten rdf:type skos:ConceptScheme.
				dataset:concepten rdfs:label "Conceptschema".
			}
		}
		
		INSERT {
			GRAPH versiongraph:version {
				dataset:concepten rdfs:isDefinedBy versiongraph:version.
				versiongraph:version rdfs:label "Concepten - BAG (publicatiedatum @CURRENTMOMENT@)".
				versiongraph:version prov:wasGeneratedBy activity:Upload.
				versiongraph:version rdf:type prov:Entity.
				versiongraph:version prov:generatedAtTime "@CURRENTMOMENT@".
				activity:Upload rdf:type prov:Activity.
				activity:Upload rdfs:label "Upload dataset @CURRENTMOMENT@".
				activity:Upload prov:startedAtTime "@CURRENTMOMENT@".
				activity:Upload prov:wasAssociatedWith concept:tomcatUser.
			}
		}
		
		INSERT {
			GRAPH usergraph:agents {
				concept:tomcatUser rdf:type prov:Agent.
				concept:tomcatUser rdfs:label "Tomcat User".
				concept:tomcatUser foaf:givenName "Tomcat User".
				concept:tomcatUser foaf:mbox "tomcat@user.org".
				concept:tomcatUser prov:actedOnBehalfOf "Apache".
			}
		}

		INSERT {
			GRAPH container:generiek {
				?s bp4mc2:signature ?sig.
			}
		}
		WHERE {
			SELECT DISTINCT ?s bif:md5(sql:group_concat(concat(?p,'=',?o),';')) as ?sig
			WHERE {
				GRAPH container:generiek {
					SELECT ?s ?p ?o
					WHERE {
						?s ?p ?o.
						?s rdf:type ?type.
						FILTER (
							?type = dmn:InformationRequirement
							|| ?type = dmn:Decision
						)
					}
					ORDER BY ?s ?p ?o
				}
            }
		}
		
		INSERT {
			GRAPH versiongraph:version {
				?s ?p ?o
			}
		}
		WHERE {
			GRAPH container:generiek {
				?s rdf:type ?type.
			    ?s ?p ?o.
			    ?s bp4mc2:signature ?sig
				FILTER (
					?type = dmn:InformationRequirement
					|| ?type = dmn:Decision
				)
			}
			FILTER NOT EXISTS {
				GRAPH doc:mastergraph {
					?s bp4mc2:signature ?sig
				}
			}
		}
		
		INSERT {
			GRAPH ?g {
				?s prov:invalidatedAtTime "@CURRENTMOMENT@"
			}
		}
		WHERE {
			GRAPH doc:mastergraph {
				?s rdfs:isDefinedBy ?g
			}
			GRAPH versiongraph:version {
				?s rdf:type ?type.
				FILTER (
					?type = dmn:InformationRequirement
					|| ?type = dmn:Decision
				)
			}
			FILTER NOT EXISTS {
				GRAPH ?g {
					?s prov:invalidatedAtTime ?time.
				}
			}
		}
		
		DELETE {
			GRAPH doc:mastergraph {
				?s ?p ?o
			}
		}
		WHERE {
			GRAPH doc:mastergraph {
				?s ?p ?o
			}
			GRAPH versiongraph:version {
				?s rdf:type ?type.
				FILTER (
					?type = dmn:InformationRequirement
					|| ?type = dmn:Decision
				)
			}
		}

		INSERT {
			GRAPH doc:mastergraph {
				?s rdf:type ?type.
				?s rdfs:isDefinedBy versiongraph:version.
				?s bp4mc2:signature ?sig
			}
		}
		WHERE {
			GRAPH versiongraph:version {
			    ?s rdf:type ?type.
			    ?s bp4mc2:signature ?sig.
				FILTER (
					?type = dmn:InformationRequirement
					|| ?type = dmn:Decision
				)
            }
		}
		
		CLEAR GRAPH container:generiek
		CLEAR GRAPH container:vragen
	''';
.